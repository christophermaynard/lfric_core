!-----------------------------------------------------------------------------
! (c) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief   Mass matrix solver on the horizontal component of a W2 field.
!> @details Applies the mass matrix solver only to the horizontal component
!!          of a W2 field. The "solved" vertical component can either be
!!          an input or left as zero.

module sci_hori_mass_matrix_solver_alg_mod

  ! Constants and types
  use constants_mod,                  only: i_def
  use integer_field_mod,              only: integer_field_type
  use field_mod,                      only: field_type
  use fs_continuity_mod,              only: W2, W2h, W2v
  use function_space_mod,             only: function_space_type
  use function_space_collection_mod,  only: function_space_collection
  use sci_geometric_constants_mod,    only: get_face_selector_ew, &
                                            get_face_selector_ns

  ! Algorithms
  use sci_mass_matrix_solver_alg_mod, only: mass_matrix_solver_alg
  use sci_split_combine_w2_alg_mod,   only: combine_w2_field_alg, &
                                            split_w2_field_alg

 implicit none

  private
  public :: hori_mass_matrix_solver_alg

contains

  ! =======================================================
  !> @brief Mass matrix solver for only the horizontal component of a W2 field
  !> @param[in,out] field_solve      The W2 solved field
  !> @param[in]     field            The W2 field to apply the horizontal mass
  !!                                 matrix solve
  !> @param[in]     vert_field_solve The optional solved vertical field. If
  !!                                 not inputted then the vertical solved
  !!                                 component is set to zero

  subroutine hori_mass_matrix_solver_alg( field_solve, field, vert_field_solve )

    implicit none

    ! Arguments
    type(field_type), intent(inout)        :: field_solve
    type(field_type), intent(in)           :: field
    type(field_type), intent(in), optional :: vert_field_solve

    ! Function spaces and fields
    type(function_space_type), pointer :: w2h_fs, w2v_fs
    type(field_type)                   :: hori_field, hori_part, hori_field_solve
    type(field_type)                   :: vert_field, vert_part
    integer(i_def) :: element_order_h
    integer(i_def) :: element_order_v

    ! Get element order
    element_order_h = field%get_element_order_h()
    element_order_v = field%get_element_order_v()
    ! Get function spaces
    w2h_fs => function_space_collection%get_fs( field%get_mesh(), &
                                                element_order_h,  &
                                                element_order_v, W2h )
    w2v_fs => function_space_collection%get_fs( field%get_mesh(), &
                                                element_order_h,  &
                                                element_order_v, W2v )
    ! Initialise fields
    call hori_field%initialise(w2h_fs)
    call hori_field_solve%initialise(w2h_fs)
    call vert_field%initialise(w2v_fs)

    ! Take horizontal part of field
    call split_w2_field_alg( hori_field, vert_field, field )
    ! Mass matrix solve of horizontal part
    call mass_matrix_solver_alg(hori_field_solve, hori_field)

    ! Convert to W2 field
    if (present(vert_field_solve)) then
      ! Combine solved horizontal part with inputted solved vertical part
      if ( vert_field_solve%which_function_space() == W2v ) then
        ! Already inputted vertical solved part
        call combine_w2_field_alg( field_solve, hori_field_solve, vert_field_solve )
      else
        ! It is a W2 field and must be split up
        call hori_part%initialise(w2h_fs)
        call vert_part%initialise(w2v_fs)
        call split_w2_field_alg( hori_part, vert_part, vert_field_solve )
        call combine_w2_field_alg( field_solve, hori_field_solve, vert_part )
      end if
    else
      ! Set solved vertical part to zero
      call vert_part%initialise(w2v_fs)
      call invoke( setval_c(vert_part, 0.0_r_def))
      call combine_w2_field_alg( field_solve, hori_field_solve, vert_part )
    end if

  end subroutine hori_mass_matrix_solver_alg

end module sci_hori_mass_matrix_solver_alg_mod