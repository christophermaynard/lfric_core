!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the UM Boundary Layer scheme

module bl_alg_mod

  use constants_mod,        only: i_def
  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  use mr_indices_mod,       only: nummr, imr_v, imr_cl, imr_ci

  ! xios output
  use io_config_mod,      only: write_diag, use_xios_io

  implicit none

  private

  public bl_alg_step

contains

  !>@brief Run the UM Boundary Layer scheme
  !>@details The UM Boundary Layer scheme does:
  !>             vertical mixing of heat, momentum and moisture,
  !>             as documented in UMDP24
  !>         NB This version uses winds in w3 space (i.e. A-grid)
  !>            and doesn't currently feed-back wind increments
  !>@param[in,out] dtheta_bl       Theta increment
  !>@param[in,out] mr              Mixing ratios
  !>@param[in]     outer           Outer loop counter
  !>@param[in]     theta           Theta in its native (wth) space
  !>@param[in]     rho             Dry density in its native (w3) space
  !>@param[in]     mr_n            Mixing ratios at time level n
  !>@param[in]     exner           Exner Pressure in the w3 space
  !>@param[in]     shear           3D wind shear on wtheta points
  !>@param[in]     delta           Edge length on wtheta points
  !>@param[in]     max_diff_smag   Maximum diffusion coefficient allowed in this run
  !>@param[in]     height_w3       Height in w3
  !>@param[in]     height_wth      Height in wth
  !>@param[in,out] derived_fields  Group of derived fields
  !>@param[in,out] cloud_fields    Group of cloud fields
  !>@param[in,out] twod_fields     Group of 2D fields
  !>@param[in,out] physics_incs    Group of physics increments
  !>@param[in,out] visc_m_blend    Blended BL-Smag diffusion coefficient for momentum
  !>@param[in,out] visc_h_blend    Blended BL-Smag diffusion coefficient for scalars
  subroutine bl_alg_step(dtheta_bl, mr, outer, theta, rho, mr_n, exner,   &
                         shear, delta, max_diff_smag,                     &
                         height_w3, height_wth,                           & 
                         derived_fields, cloud_fields, twod_fields,       &
                         physics_incs, visc_m_blend, visc_h_blend)

    use bl_kernel_mod, only: bl_kernel_type

    implicit none

    type( field_type ), intent( inout )     :: dtheta_bl, mr(nummr)
    type( field_type ), intent( in )        :: theta, rho, exner, mr_n(nummr)
    type( field_type ), intent( in )        :: height_w3, height_wth
    type( field_type ), intent( in )        :: shear, delta, max_diff_smag
    type( field_collection_type ), intent(inout) :: derived_fields
    type( field_collection_type ), intent(inout) :: cloud_fields
    type( field_collection_type ), intent(inout) :: twod_fields
    type( field_collection_type ), intent(inout) :: physics_incs
    type( field_type ), intent( inout )     :: visc_m_blend, visc_h_blend
    integer(kind=i_def), intent(in)         :: outer

    ! Temporary fields unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth  => null()
    type( field_type ), pointer :: wetrho_in_wth => null()
    type( field_type ), pointer :: wetrho_in_w3 => null()
    type( field_type ), pointer :: u1_in_w3 => null()
    type( field_type ), pointer :: u2_in_w3  => null()
    type( field_type ), pointer :: w_physics => null()
    type( field_type ), pointer :: theta_star => null()
    type( field_type ), pointer :: u1_in_w3_star => null()
    type( field_type ), pointer :: u2_in_w3_star => null()
    type( field_type ), pointer :: w_physics_star => null()
    type( field_type ), pointer :: tstar_2d  => null()
    type( field_type ), pointer :: zh_2d => null()
    type( field_type ), pointer :: z0msea_2d => null()
    type( field_type ), pointer :: ntml_2d => null()
    type( field_type ), pointer :: cumulus_2d => null()
    type( field_type ), pointer :: cf_area  => null()
    type( field_type ), pointer :: cf_ice  => null()
    type( field_type ), pointer :: cf_liq  => null()
    type( field_type ), pointer :: cf_bulk  => null()
    type( field_type ), pointer :: rh_crit_wth => null()
    type( field_type ), pointer :: dt_bl => null()
    type( field_type ), pointer :: dmv_bl => null()
    type( field_type ), pointer :: dt_conv => null()
    type( field_type ), pointer :: dmv_conv => null()
    type( field_type ), pointer :: dtl_mphys => null()
    type( field_type ), pointer :: dmt_mphys => null()
    type( field_type ), pointer :: sw_heating_rate => null()
    type( field_type ), pointer :: lw_heating_rate => null()

    ! Unpack derived fields
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    wetrho_in_wth => derived_fields%get_field('wetrho_in_wth')
    wetrho_in_w3 => derived_fields%get_field('wetrho_in_w3')
    u1_in_w3 => derived_fields%get_field('u1_in_w3')
    u2_in_w3 => derived_fields%get_field('u2_in_w3')
    w_physics => derived_fields%get_field('w_physics')
    theta_star => derived_fields%get_field('theta_star')
    u1_in_w3_star => derived_fields%get_field('u1_in_w3_star')
    u2_in_w3_star => derived_fields%get_field('u2_in_w3_star')
    w_physics_star => derived_fields%get_field('w_physics_star')

    ! Unpack twod fields
    tstar_2d => twod_fields%get_field('tstar')
    zh_2d => twod_fields%get_field('zh')
    z0msea_2d => twod_fields%get_field('z0msea')
    ntml_2d => twod_fields%get_field('ntml')
    cumulus_2d => twod_fields%get_field('cumulus')

    ! Unpack cloud fields
    cf_area => cloud_fields%get_field('area_fraction')
    cf_ice => cloud_fields%get_field('ice_fraction')
    cf_liq => cloud_fields%get_field('liquid_fraction')
    cf_bulk => cloud_fields%get_field('bulk_fraction')
    rh_crit_wth => cloud_fields%get_field('rh_crit_wth')
    
    ! Unpack the physics increments
    dt_bl => physics_incs%get_field('dt_bl')
    dmv_bl => physics_incs%get_field('dmv_bl')
    dt_conv => physics_incs%get_field('dt_conv')
    dmv_conv => physics_incs%get_field('dmv_conv')
    dtl_mphys => physics_incs%get_field('dtl_mphys')
    dmt_mphys => physics_incs%get_field('dmt_mphys')
    sw_heating_rate => physics_incs%get_field('sw_heating_rate')
    lw_heating_rate => physics_incs%get_field('lw_heating_rate')

    call invoke(bl_kernel_type( outer, theta, rho, wetrho_in_w3,              &
                                wetrho_in_wth, exner, exner_in_wth,           &
                                u1_in_w3, u2_in_w3, w_physics,                &
                                mr_n(imr_v), mr_n(imr_cl), mr_n(imr_ci),      &
                                theta_star,                                   &
                                u1_in_w3_star, u2_in_w3_star, w_physics_star, &
                                height_w3, height_wth,                        & 
                                shear, delta, max_diff_smag,                  &
                                tstar_2d, zh_2d,                              &
                                z0msea_2d, ntml_2d, cumulus_2d, dtheta_bl,    &
                                dt_bl, dmv_bl, dt_conv, dmv_conv,             &
                                dtl_mphys, dmt_mphys,                         &
                                sw_heating_rate, lw_heating_rate,             &
                                mr(imr_v), mr(imr_cl),mr(imr_ci), cf_area,    &
                                cf_ice, cf_liq, cf_bulk, rh_crit_wth,         &
                                visc_m_blend, visc_h_blend ) )

    if (write_diag .and. use_xios_io) then
      call dt_bl%write_field('dt_bl')
      call dmv_bl%write_field('dmv_bl')
    end if

  end subroutine bl_alg_step

end module bl_alg_mod
